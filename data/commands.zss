# ---------------------------------------------
# This file holds the common commands for MK characters.
[StateDef -1]

# ---------------------------------------------
# Run
if !const(OpenMK.Char.DisableRun) { # If it isn't disabled.
	if power > 0 && command = "run" && command = "holdfwd" && command != "holdback" && command != "holdup" && stateno != 100 && ctrl && stateno != 180 {
		if statetype = S {
			changestate{value: 100}
		}
	}
}

# ---------------------------------------------
# Block
if ctrl && stateno != 180 && stateno != [120,140] && command = "block" {
	if statetype = S || statetype = C {
		changestate{value: 120}
	}
}

# ---------------------------------------------
# Back+Attack ones
if command = "back_z" { # Roundhouse Kick
	if statetype = S && ctrl && stateno != 180 {
		changestate{value: 240}
	}
} else if command = "back_c" { # Sweep Kick
	if statetype = S && ctrl && stateno != 180 {
		changestate{value: 250}
	}
}

# ---------------------------------------------
# Punches
if statetype = S && ctrl && stateno != 180 {
	if command = "x" {
		changestate{value: 200}
	} else if command = "a" {
		changestate{value: 220}
	}
}

# ---------------------------------------------
# Kicks
if statetype = S && ctrl && stateno != 180 {
	if command = "z" {
		changestate{value: 210}
	} else if command = "c" {
		changestate{value: 230}
	}
}

# ---------------------------------------------
# Crouch Attacks
if statetype = C && ctrl && stateno != 180 {
	if command = "z" { # Crouch HK
		changestate{value: 400}
	} else if command = "x" { # Uppercut
		changestate{value: 410}
	} else if command = "a" { # Crouch LP
		changestate{value: 420}
	} else if command = "c" { # Crouch LK
		changestate{value: 430}
	}
}

# ---------------------------------------------
# Air Kicks
if vel x = 0 { # Neutral one
	if statetype = A && ctrl && stateno != 180 && (command = "c" || command = "z") {
		changestate{value: 600}
	}
} else { # Leaping one
	if statetype = A && ctrl && stateno != 180 && (command = "c" || command = "z") {
		changestate{value: 620}
	}
}
	
# ---------------------------------------------
# Air Punch
if (command = "x" || command = "a") && statetype = A && ctrl && stateno != 180 {
	changestate{value: 610}
}