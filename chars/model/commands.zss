# ---------------------------------------------
# This file holds the common commands for MK characters.
[StateDef -1]

# ---------------------------------------------
# Run
if const(OpenMK.Char.EnableRun) > 0 { # Only if it's enabled.
	if power > 0 && command = "run" && command = "holdfwd" && command != "holdback" && command != "holdup" && stateNo != const(StateRunForward) && ctrl && stateNo != const(StateWin) {
		if stateType = S {
			selfState{value: const(StateRunForward)}
		}
	}
}

# ---------------------------------------------
# Block
if ctrl && stateNo != const(StateWin) && stateNo != [const(StateGuard_start),const(StateGuard_end)] && command = "block" {
	if stateType = S || stateType = C {
		selfState{value: const(StateGuard_start)}
	}
}

# ---------------------------------------------
# Back+Attack ones
if command = "holdback" && command = "z" { # Roundhouse Kick
	if stateType = S && ctrl && stateNo != const(StateWin) {
		selfState{value: const(StateRoundhouseKick)}
	}
} else if command = "holdback" && command = "c" { # Sweep Kick
	if stateType = S && ctrl && stateNo != const(StateWin) {
		selfState{value: const(StateSweep)}
	}
}

# ---------------------------------------------
# Close Hits/Dial-a-Kombos
if stateType = S && ctrl && stateNo != const(StateWin) {
	if p2StateType != A && p2BodyDist x <= 20 {
		if command = "x" {
			selfState{value: const(StateClosePunch)}
		} else if (command = "c" || command = "z") {
			selfState{value: const(StateCloseKick)}
		}
	}
}

# ---------------------------------------------
# Punches
if stateType = S && ctrl && stateNo != const(StateWin) {
	if command = "x" {
		selfState{value: const(StateHighPunchI)}
	} else if command = "a" {
		selfState{value: const(StateLowPunchI)}
	}
}

# ---------------------------------------------
# Kicks
if stateType = S && ctrl && stateNo != const(StateWin) {
	if command = "z" {
		selfState{value: const(StateHighKick)}
	} else if command = "c" {
		selfState{value: const(StateLowKick)}
	}
}

# ---------------------------------------------
# Crouch Attacks
if stateType = C && ctrl && stateNo != const(StateWin) {
	if command = "z" { # Crouch HK
		selfState{value: const(StateCrouchHighKick)}
	} else if command = "x" { # Uppercut
		selfState{value: const(StateUppercut)}
	} else if command = "a" { # Crouch LP
		if const(OpenMK.Char.CrouchAttacks) = 0 {
			selfState{value: const(StateUppercut)}
		} else if const(OpenMK.Char.CrouchAttacks) = 1 {
			selfState{value: const(StateCrouchLowPunch)}
		}
	} else if command = "c" { # Crouch LK
		if const(OpenMK.Char.CrouchAttacks) = 0 {
			selfState{value: const(StateCrouchHighKick)}
		} else if const(OpenMK.Char.CrouchAttacks) = 1 {
			selfState{value: const(StateCrouchLowKick)}
		}
	}
}

# ---------------------------------------------
# Air Kicks
if vel x = 0 { # Neutral one
	if stateType = A && ctrl && stateNo != const(StateWin) && (command = "c" || command = "z") {
		selfState{value: const(StateAirKick)}
	}
} else { # Leaping one
	if stateType = A && ctrl && stateNo != const(StateWin) && (command = "c" || command = "z") {
		selfState{value: const(StateLeapKick)}
	}
}

# ---------------------------------------------
# Air Punch
if (command = "x" || command = "a") && stateType = A && ctrl && stateNo != const(StateWin) {
	selfState{value: const(StateAirPunch)}
}